plugins {
    id 'java'
    id 'war'
    id 'maven-publish'
}

publishing {
    publications {
        library(MavenPublication) {
            // from components.java
            from components.web
        }
    }
    repositories {
        maven {
            url "https://gitlab.cern.ch/api/v4/projects/209928/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = System.getenv("GITLAB_TOKEN") ?: project.findProperty("gitLabPrivateToken")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

group = 'gov.fnal'
version = '4.0'
// Set java version for compilation compatibility
// sourceCompatibility = '21'
ext {
    appName = 'Frontier'
    appVersion = '4.0'
    catalinaHome = System.getenv('CATALINA_HOME') ?: '/path/to/tomcat'
}

repositories {
    mavenCentral()
}

configurations.all {
    exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
}

dependencies {
    // Logging
    implementation('ch.qos.logback:logback-classic:1.4.7') {
        exclude module: 'module-info'
    }
    //implementation 'org.slf4j:jul-to-slf4j:2.0.9'
    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:jul-to-slf4j:2.0.7'  // For the bridge

    // Oracle JDBC driver (include in WAR)
    implementation 'com.oracle.database.jdbc:ojdbc11:21.11.0.0'  // Explicit version
    // Logstash
    runtimeOnly 'net.logstash.logback:logstash-logback-encoder:7.4'
    // Jakarta Servlet API (Tomcat 11 uses Jakarta EE 10)
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    // Add dependencies here, e.g., servlet API
    // providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    testImplementation 'junit:junit:4.13.2'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

// Define reusable paths using layout API
def distDir = layout.buildDirectory.dir('dist')
def webDir = layout.buildDirectory.dir('web')
def testDataDir = layout.buildDirectory.dir('test/data')
def testReportsDir = layout.buildDirectory.dir('test/reports')


war {
    archiveBaseName.set(appName)
    archiveVersion.set('')
    destinationDirectory.set(distDir)
    webAppDirName = 'web'
    from configurations.runtimeClasspath.collect {
        it.isDirectory() ? it : zipTree(it)
    }
}


tasks.register('prepare', Copy) {
    from 'web'
    into webDir
}

tasks.register('customClean', Delete) {
    delete layout.buildDirectory
}


tasks.register('testinit') {
    doLast {
        delete testDataDir, testReportsDir
        mkdir testDataDir
        mkdir testReportsDir
    }
}

tasks.register('testcompile', Copy) {
    dependsOn 'classes', 'testinit'
    from 'src/test'
    into layout.buildDirectory.dir('test')
    exclude '**/*.java'
}

tasks.test {
    useJUnit()
    reports {
        junitXml.required.set(true)
        junitXml.outputLocation.set(testDataDir)
        html.required.set(true)
        html.outputLocation.set(testReportsDir)
    }
}

tasks.register('dist', Copy) {
    dependsOn 'classes'
    from 'docs'
    into distDir.dir('docs')

    doLast {
        def warFile = war.archiveFile.get().asFile
        warFile.copyTo(distDir.file("${appName}.war").get().asFile)
    }
}

tasks.register('localinstall', Copy) {
    dependsOn 'dist'
    from distDir.file("${appName}.war")
    into file("$catalinaHome/webapps")
}

tasks.register('localuninstall', Delete) {
    delete file("$catalinaHome/webapps/${appName}.war")
    delete file("$catalinaHome/webapps/${appName}")
}

